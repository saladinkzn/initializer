buildscript {
	repositories {
		maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
	}

	dependencies {
		classpath 'ru.shadam.augmentor:augmentor:0.1-SNAPSHOT'
	}
}

def rootProject = project

subprojects {
	repositories {
		jcenter()
	}

	group = rootProject.group
	version = rootProject.version

	apply plugin: 'java'

	compileJava {
		sourceCompatibility = '1.7'
		targetCompatibility = '1.7'
		options.encoding = 'UTF-8'
	}

	dependencies {
		testCompile 'junit:junit:4.11'
	}
}

project(':core') {
	dependencies {
		compile 'org.freemarker:freemarker:2.3.20'
	}
}

project(':plugins') {
	dependencies {
		compile project(':core')
	}
}

project(':web') {
	apply plugin: 'application'
	mainClassName = 'ru.shadam.initializer.web.Program'

	dependencies {
		compile project(':core')
		compile project(':plugins')

		compile 'org.springframework.boot:spring-boot-starter-web:1.2.0.RC2'
		compile 'org.springframework.boot:spring-boot-starter-freemarker:1.2.0.RC2'
		compile 'org.webjars:bootstrap:3.3.1'
	}

	task 're-run'(type: ru.shadam.augmentor.AugmentorTask) {
		innerTask = 'run'
	}
}


task wrapper(type: Wrapper) {
	gradleVersion = '2.1'
}
